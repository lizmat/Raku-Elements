#!/usr/bin/env raku

# Make sure we get the most recent version
use lib $*PROGRAM.parent;
use Raku::Elements;

# This script reads the files in the resources directory, and updates
# the doc/Raku-Elements.rakudoc file with it

my $groups-io := $*PROGRAM.sibling("resources").add("groups");

my $generator := $*PROGRAM-NAME;
my $generated := DateTime.now.gist.subst(/\.\d+/,'');
my $start     := '=head1 GROUPS';
my $end       := '=head1 CLASSES';

# slurp the whole file and set up writing to it
my $file := $*PROGRAM.sibling("doc").add("Raku-Elements.rakudoc");
my @lines = $file.lines;  # must be greedy
$*OUT = $file.open(:w);

# for all the lines in the doc that don't need special handling
while @lines {
    my $line := @lines.shift;

    # nothing to do yet
    unless $line.starts-with($start) {
        say $line;
        next;
    }

    say $start;

    say "\n=comment Generated on $generated by $generator";
    say "=comment PLEASE DON'T CHANGE ANYTHING BELOW THIS LINE\n";

    # skip the old version of the doc
    while @lines {
        last if @lines.shift.starts-with($end);
    }

    my sub backtick($name) {
        $name.starts-with("«") ?? "C<$name>" !! "C«$name»"
    }

    for $groups-io.dir.sort -> $io {
        say "=head2 $io.basename()\n";
        for Raku::Elements.elements-from-io($io) {
            say "\n=head3 &backtick($_.name) $_.tagline()";
            if .alternates -> @alternates {
                say "=item alternates: @alternates.map(&backtick)";
            }
            say "=item tags: $_.tags()";
            say "\n$_.description()" if $_.description;
            say "";
        }
    }

    say "\n=comment PLEASE DON'T CHANGE ANYTHING ABOVE THIS LINE\n";

    # we're done
    say $end;
}

# close the file properly
$*OUT.close;

# vim: expandtab shiftwidth=4
